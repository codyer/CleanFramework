apply plugin: 'com.android.application'

// 产品名
def MODULE_NAME = "CleanApp"

// 打包时间
def packageTime() {
    return new Date().format("yyyyMMdd_HHmm", TimeZone.getTimeZone("GMT+8"))
}
//加载签名配置的文件
Properties props = new Properties()
props.load(new FileInputStream(file("signing.properties")))

android {

    // 签名配置
    signingConfigs {
        DefaultConfig {
            keyAlias props['KEY_ALIAS']
            keyPassword props['KEY_PASSWORD']
            storeFile file(props['KEYSTORE_FILE'])
            storePassword props['KEYSTORE_PASSWORD']
        }
    }

    dataBinding {
        enabled true
    }

    // 编译sdk版本
    compileSdkVersion rootProject.ext.compileSdkVersion

    // 构建工具版本
    buildToolsVersion rootProject.ext.buildToolsVersion

    // 主要是用来设置Library发布所有的variants
//    publishNonDefault true

    // 资源前缀
//    resourcePrefix MODULE_NAME + '_'

    // 默认配置
    defaultConfig {
        applicationId "com.cody.app" // apk包名
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName // 版本号
        versionNameSuffix '_' + MODULE_NAME
        // dex突破65535的限制
        multiDexEnabled true
        // Android 单元测试 test runner
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    // 构建类型，此处配置debug和release版本的一些参数，像混淆、签名配置。
    buildTypes {
        // release 包的配置
        release {
            // 启用资源压缩 因为您可能需要编辑 proguard-rules.pro 文件以保留动态创建或调用的类或方法，然后再开始移除资源
//            shrinkResources true
            // 开启代码压缩
            minifyEnabled false
            // 对齐
            zipAlignEnabled true
            // 指定混淆文件
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            // 指定签名配置
            signingConfig signingConfigs.DefaultConfig
        }
    }
    // 产品特性
    productFlavors {
        dev { void }
        uat { void }
        uat1 { void }
        stg { void }
        pro { void }

        // 自动替换AndroidManifest.xml中的渠道号，同时根据产品特性修改应用名
        productFlavors.all { flavor ->
            if (name.equals("pro")) {
                flavor.manifestPlaceholders = [APP_BUILD_TYPE: "@string/app_name", CHANNEL_ID: name]
            } else {
                flavor.manifestPlaceholders = [APP_BUILD_TYPE: "Clean_" + name, CHANNEL_ID: name]
            }
        }
    }

    // 打包后自动修改apk的名字
    // release 包的命名格式为：产品名_版本号_渠道号_release_打包时间.apk
    // debug 包的命名格式为：产品名_版本号_渠道号_debug_打包时间.apk
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def outputFile = output.outputFile
            if (null != outputFile && outputFile.name.endsWith('.apk')) {
                File outputDir = new File(outputFile.parent + "/${variant.buildType.name}");
                def baseName = MODULE_NAME + "_${defaultConfig.versionName}" +
                        "_" + variant.productFlavors[0].name
                def newApkName = baseName + "_" + variant.buildType.name + "_${packageTime()}.apk"

                /*  if (variant.buildType.name.equals('release')) {
                      newApkName = baseName + '_Release.apk'
                  } else if (variant.buildType.name.equals('debug')) {
                      newApkName = baseName + "_Debug_${packageTime()}.apk"
                  }*/

                output.outputFile = new File(outputDir, newApkName)
            }
        }
    }
    return void
}

// 多渠道配置
configurations {
    devDebugCompile
    uatDebugCompile
    uat1DebugCompile
    stgDebugCompile
    proDebugCompile

    devReleaseCompile
    uatReleaseCompile
    uat1ReleaseCompile
    stgReleaseCompile
    proReleaseCompile
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    debugCompile project(path: ':xfoundation', configuration: 'debug')
    releaseCompile project(path: ':xfoundation', configuration: 'release')

    devDebugCompile project(path: ':handler', configuration: 'devDebug')
    uatDebugCompile project(path: ':handler', configuration: 'uatDebug')
    uat1DebugCompile project(path: ':handler', configuration: 'uat1Debug')
    stgDebugCompile project(path: ':handler', configuration: 'stgDebug')
    proDebugCompile project(path: ':handler', configuration: 'proDebug')

    devReleaseCompile project(path: ':handler', configuration: 'devRelease')
    uatReleaseCompile project(path: ':handler', configuration: 'uatRelease')
    uat1ReleaseCompile project(path: ':handler', configuration: 'uat1Release')
    stgReleaseCompile project(path: ':handler', configuration: 'stgRelease')
    proReleaseCompile project(path: ':handler', configuration: 'proRelease')
    compile 'com.android.support:appcompat-v7:24.2.1'
    testCompile 'junit:junit:4.12'
}
